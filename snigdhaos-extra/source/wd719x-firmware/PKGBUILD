# Maintainer: CELESTIFYX Team <celestifyx@gmail.com>

pkgname="wd719x-firmware"
pkgver=1.0.0
pkgrel=1
pkgdesc="Driver for Western Digital WD7193, WD7197 and WD7296 SCSI cards"
url="https://github.com/mijailr/wd719x-firmware"
license=("GPL-3.0-or-later")
arch=("any")

source=("https://github.com/mijailr/wd719x-firmware/raw/master/pciscsi.exe")
sha512sums=("d85f9f2462bd665b264ecf2f09eacfc206099ab0a053f9c0b25417cb0a964de8f0d77d70f8ef9d3a5341d02af23ba3085fff4ec393939704afcdcc9cd37987d5")

makedepends=("lha")
noextract=("pciscsi.exe")

package() {
    lha l --convert-filename-case pciscsi.exe 2> /dev/null && local lha_opts="--convert-filename-case"

    lha xi $lha_opts pciscsi.exe  pci-scsi.exe
    lha xi $lha_opts pci-scsi.exe nt/wd7296a.sys

    dd if=wd7296a.sys of=wd719x-risc.bin bs=1 skip=5760  count=14336
    dd if=wd7296a.sys of=wd719x-wcs.bin  bs=1 skip=20096 count=514

    install -Dm644 "${srcdir}/wd719x-risc.bin" "${pkgdir}/usr/lib/firmware/wd719x-risc.bin"
    install -Dm644 "${srcdir}/wd719x-wcs.bin"  "${pkgdir}/usr/lib/firmware/wd719x-wcs.bin"
}
