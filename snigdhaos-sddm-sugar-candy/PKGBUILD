# Maintainer: Eshan Roy <m.eshanized@gmail.com>
pkgname=snigdhaos-sddm-sugar-candy
pkgver=r1.3dabe46
pkgrel=1
pkgdesc="Sugar Candy is the sweetest login theme available for the SDDM display manager."
arch=('any')
url='https://store.kde.org/p/1312658/'
license=('GPL3')
depends=('sddm' 'qt5-graphicaleffects' 'qt5-quickcontrols2' 'qt5-svg')
makedepends=('git')
provides=("sddm-sugar-candy")
conflicts=("sddm-sugar-candy")
backup=('usr/share/sddm/themes/snigdhaos-sugar-candy/theme.conf')
source=('snigdhaos-sugar-candy::git+https://github.com/Snigdha-OS/snigdhaos-sugar-candy.git')
sha256sums=('SKIP')

pkgver() {
  cd "$srcdir/snigdhaos-sugar-candy"
    printf "r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

package() {
  cd "$srcdir/snigdhaos-sugar-candy"
  install -Dm644 "Main.qml" "$pkgdir/usr/share/sddm/themes/snigdhaos-sugar-candy/Main.qml"
  install -Dm644 "theme.conf" "$pkgdir/usr/share/sddm/themes/snigdhaos-sugar-candy/theme.conf"
  install -Dm644 "metadata.desktop" "$pkgdir/usr/share/sddm/themes/snigdhaos-sugar-candy/metadata.desktop"
  find ./Assets -type f -exec install -Dm644 {} "$pkgdir/usr/share/sddm/themes/snigdhaos-sugar-candy/{}" \;
  find ./Backgrounds -type f -exec install -Dm644 {} "$pkgdir/usr/share/sddm/themes/snigdhaos-sugar-candy/{}" \;
  find ./Components -type f -exec install -Dm644 {} "$pkgdir/usr/share/sddm/themes/snigdhaos-sugar-candy/{}" \;
#   install -Dm 644 "COPYING" "${pkgdir}/usr/share/licenses/${pkgname}/COPYING"
}
