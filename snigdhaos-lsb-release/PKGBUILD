# Maintainer : RiO <d3v1l0n@outlook.in>

pkgname=snigdhaos-lsb-release
pkgver=1
_commit=a25a4fcd73c79bd5af0dd8d948a7c96dcbfd2d07
pkgrel=1
pkgdesc='LSB version query program for Snigdha OS'
arch=('any')
url='https://refspecs.linuxfoundation.org/lsb.shtml'
license=('GPL-2.0-or-later')
depends=('sh')
makedepends=('git')
source=("git+https://github.com/LinuxStandardBase/lsb-samples.git#commit=${_commit}"
        'lsb-release'
        'lsb_release_make_man_page_reproducible.patch')
sha256sums=('SKIP'
            'SKIP'
            'SKIP')

pkgver() {
	cd lsb-samples/lsb_release/src
	printf '%s.r%s.%s' "$(grep -Po 'SCRIPTVERSION="\K[^"]*' lsb_release)" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
	patch -d lsb-samples/lsb_release/src -Np1 -i "${srcdir}/lsb_release_make_man_page_reproducible.patch"
}

build() {
	make -C lsb-samples/lsb_release/src
}

package() {
	install -D -m755 lsb-samples/lsb_release/src/lsb_release -t "${pkgdir}/usr/bin"
	install -D -m644 lsb-samples/lsb_release/src/lsb_release.1.gz -t "${pkgdir}/usr/share/man/man1"
	install -D -m644 lsb-release -t "${pkgdir}/etc"
}
